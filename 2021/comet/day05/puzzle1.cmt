class Line {
    init(x1, y1, x2, y2) {
        self.x1 = x1
        self.y1 = y1
        self.x2 = x2
        self.y2 = y2
        if (x1 < x2) {
            self.x_min = x1
            self.x_max = x2
        } else {
            self.x_min = x2
            self.x_max = x1
        }
        if (y1 < y2) {
            self.y_min = y1
            self.y_max = y2
        } else {
            self.y_min = y2
            self.y_max = y1
        }
    }
}

class Grid {
    init(width, height) {
        self.board = List((width*height)+1)
        for (var i = 0; i <= (width*height); i += 1) {
            self.board[i] = 0
        }
        self.width = width
        self.height = height
    }

    _mark(x, y) {
        self.board[(self.width * y) + x] += 1 
    }

    add_line(line) {
        if (line.x1 == line.x2 || line.y1 == line.y2) {
            for (var x = line.x_min; x <= line.x_max; x += 1) {
                for (var y = line.y_min; y <= line.y_max; y += 1) {
                    self._mark(x, y)
                }
            }
        }
    }

    count_overlaps() {
        return self.board.reduce(0, |acc, item, index| {
            if (item > 1) {
                acc += 1
            }
            return acc
        })
    }

    print_board() {
        for (var y = 0; y < self.height; y += 1) {
            var line = ''
            for (var x = 0; x < self.width; x += 1) {
                line += self.board[(self.width * y) + x].to_string()
            }
            print(line)
        }
    }
}

var lines = []
var input_lines = File.read_all_lines('input')
foreach(var input in input_lines) {
    var coords = input.trim().split(' -> ')
    var ones = coords[0].split(',').map(|x| {
        return Number.parse(x)
    })
    var twos = coords[1].split(',').map(|x| {
        return Number.parse(x)
    })
    lines.append(Line(ones[0], ones[1], twos[0], twos[1]))
}

var max_x1 = lines.max(|x| {
    return x.x1
})
var max_x2 = lines.max(|x| {
    return x.x2
})
var max_y1 = lines.max(|x| {
    return x.y1
})
var max_y2 = lines.max(|x| {
    return x.y2
})
var width = [max_x1, max_x2].max() + 1
var height = [max_y1, max_y2].max() + 1

var grid = Grid(width, height)
foreach(var line in lines) {
    grid.add_line(line)
}

# grid.print_board()

print(grid.count_overlaps())